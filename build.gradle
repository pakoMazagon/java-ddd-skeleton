// Common for all projects
allprojects {
  apply plugin: 'java'

  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    mavenCentral()
  }

  dependencies {
    // Prod
    implementation 'org.projectlombok:lombok:1.18.20'
    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
  }

  test {
    useJUnitPlatform()

    testLogging {
      events "passed", "skipped", "failed"
    }
  }


  task view_paths {
    doLast { task ->
      println "$task.project.name"
      println "------------------"
      println "Main: $sourceSets.main.java.srcDirTrees"
      println "   Resources: $sourceSets.main.resources.srcDirTrees"
      println "Tests: $sourceSets.test.java.srcDirTrees"
      println "   Resources: $sourceSets.test.resources.srcDirTrees"
    }
  }
}

// All subprojects (located in src/*)
subprojects {
  group = "tv.codely.${rootProject.name}"

  sourceSets {
    main {
      java { srcDirs = ['main'] }
      resources { srcDirs = ['main/resources'] }
    }

    test {
      java { srcDirs = ['test'] }
      resources { srcDirs = ['test/resources'] }
    }
  }
}

// Main project (located in app/)
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:2.7.10")
  }
}

sourceSets {
  main {
    java { srcDirs = ['app/main'] }
    resources { srcDirs = ['app/main/resources'] }
  }

  test {
    java { srcDirs = ['app/test'] }
    resources { srcDirs = ['app/test/resources'] }
  }
}

apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
  archiveBaseName = 'java-ddd-skeleton'
  archiveFileName = 'java-ddd-skeleton.jar'
  archiveVersion = '0.0.1'
}

apply plugin: 'application'
mainClassName = 'tv.codely.Starter'

dependencies {
  implementation("org.springframework.boot:spring-boot-starter-web")

  implementation project(":backoffice")
  implementation project(":mooc")

  testImplementation("org.springframework.boot:spring-boot-starter-test")
}
